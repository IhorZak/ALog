apply plugin: "com.android.application"

android {
    buildToolsVersion versions.buildTools

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion versions.sampleSdk.compile

    def versionProperties = new Properties()
    versionProperties.load(new FileInputStream("version.properties"))

    defaultConfig {
        applicationId "ua.pp.ihorzak.alog.sample"

        minSdkVersion versions.sampleSdk.min
        targetSdkVersion versions.sampleSdk.target

        versionCode Integer.parseInt(versionProperties.getProperty("VERSION_CODE"))
        versionName versionProperties.getProperty("VERSION_NAME")
    }

    signingConfigs {
        release {
            storeFile file("sample.keystore")
            storePassword "alogsample"
            keyAlias "release"
            keyPassword "alogsample"
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }

        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = "alog-sample-" + variant.buildType.name + "-" +
                    defaultConfig.versionName + "-" + defaultConfig.versionCode + ".apk"
        }
    }
}

dependencies {
    implementation project(":library")
    implementation "com.android.support:appcompat-v7:$versions.sampleDependencies.appCompat"

    testImplementation "junit:junit:$versions.sampleTestDependencies.jUnit"
}
