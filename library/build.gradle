apply plugin: "com.android.library"
apply plugin: "com.jfrog.bintray"
apply plugin: "com.github.dcendents.android-maven"

def localProperties = new Properties()
localProperties.load(rootProject.file("local.properties").newDataInputStream())

def versionProperties = new Properties()
versionProperties.load(new FileInputStream("version.properties"))

def siteUrl = "https://github.com/IhorZak/ALog"
def gitUrl = "https://github.com/IhorZak/ALog.git"

project.archivesBaseName = "alog"
group "ua.pp.ihorzak"
version versionProperties.getProperty("VERSION_NAME")

android {
    buildToolsVersion versions.buildTools

    compileSdkVersion versions.sdk.compile

    compileOptions {
        sourceCompatibility versions.javaVersions.source
        targetCompatibility versions.javaVersions.target
    }

    defaultConfig {
        minSdkVersion versions.sdk.min
        targetSdkVersion versions.sdk.target

        versionCode Integer.parseInt(versionProperties.getProperty("VERSION_CODE"))
        versionName versionProperties.getProperty("VERSION_NAME")
    }

    testOptions.unitTests.includeAndroidResources = true
}

dependencies {
    testImplementation "junit:junit:$versions.testDependencies.jUnit"
    testImplementation "org.robolectric:robolectric:$versions.testDependencies.robolectric"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = "sources"
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging "aar"
                name "ALog"
                url siteUrl

                licenses {
                    license {
                        name "The Apache Software License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        name "Ihor Zakhozhyi"
                        email "ihorzak@gmail.com"
                    }
                }
            }
        }
    }
}

bintray {
    user = localProperties.getProperty("bintray.user")
    key = localProperties.getProperty("bintray.key")

    configurations = ["archives"]

    pkg {
        repo = "maven"
        name = "ALog"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
    }
}
