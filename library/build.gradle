apply plugin: "com.android.library"
apply plugin: "maven-publish"
apply plugin: "signing"

def localProperties = new Properties()
localProperties.load(rootProject.file("local.properties").newDataInputStream())

def versionProperties = new Properties()
versionProperties.load(rootProject.file("version.properties").newDataInputStream())

project.archivesBaseName = "alog"
group "ua.pp.ihorzak"
version versionProperties.getProperty("VERSION_NAME")

android {
    buildToolsVersion versions.buildTools

    compileSdkVersion versions.sdk.compile

    compileOptions {
        sourceCompatibility versions.javaVersions.source
        targetCompatibility versions.javaVersions.target
    }

    defaultConfig {
        minSdkVersion versions.sdk.min
        targetSdkVersion versions.sdk.target

        versionCode Integer.parseInt(versionProperties.getProperty("VERSION_CODE"))
        versionName versionProperties.getProperty("VERSION_NAME")
    }

    testOptions.unitTests.includeAndroidResources = true
}

dependencies {
    testImplementation "junit:junit:$versions.testDependencies.jUnit"
    testImplementation "org.robolectric:robolectric:$versions.testDependencies.robolectric"
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

signing {
    useInMemoryPgpKeys(
            localProperties.getProperty("signingKeyId"),
            localProperties.getProperty("signingKey"),
            localProperties.getProperty("signingPassword")
    )
    sign publishing.publications
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId "ua.pp.ihorzak"
                artifactId "alog"
                version versionProperties.getProperty("VERSION_NAME")

                artifact sourcesJar
                artifact javadocJar

                from components.release

                pom {
                    name = "ALog"
                    description = "Simple Android logger based on android.util.Log"
                    url = "https://github.com/IhorZak/ALog"

                    licenses {
                        license {
                            name = "The Apache Software License, Version 2.0"
                            url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }

                    developers {
                        developer {
                            name = "Ihor Zakhozhyi"
                            email = "ihorzak@gmail.com"
                        }
                    }

                    scm {
                        connection = "scm:git:github.com/IhorZak/ALog.git"
                        developerConnection = "scm:git:ssh://github.com/IhorZak/ALog.git"
                        url = "https://github.com/IhorZak/ALog/tree/master"
                    }
                }
            }
        }
    }
}